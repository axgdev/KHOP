buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.6.2"
    }
}

group 'axelg'
version '0.50.0'

apply plugin: 'kotlin'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task allJar(type: Jar) {
    //Include source files of library into sources folder
    into 'sources', { from sourceSets.main.allSource }
    //Include all dependencies
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    classifier = 'all'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task withSources(type: Jar) {
    //Include source files of library into sources folder
    into 'sources', { from sourceSets.main.allSource }
    //Include all compiled files without dependencies
    from sourceSets.main.output
    classifier = 'withsources'
}

artifacts {
    archives sourcesJar
    archives allJar
    archives withSources
}

artifactory {
    contextUrl = artifactory_local_url
    publish {
        repository {
            repoKey = artifactory_repo_key
            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('mavenJava')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact withSources
        }
    }
}
